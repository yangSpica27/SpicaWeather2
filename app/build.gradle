import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'dagger.hilt.android.plugin'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
    id 'AndResGuard'
}

android {
    compileSdk 34

    defaultConfig {
        applicationId "me.spica.weather2"
        minSdk 24
        targetSdk 33
        versionCode 1
        versionName "0.1"

        renderscriptTargetApi 24
        renderscriptSupportModeEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        resConfigs 'en'

        ndk {
            //设置支持的SO库架构（开发者可以根据需要，选择一个或多个平台的so）
            abiFilters "arm64-v8a"
        }

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }

        buildConfigField 'String', 'CAIYUN', quotWrapper("mOQ3KNVG1rXoCH75")
    }

    signingConfigs {
        signingConfig {
            storeFile rootProject.file("key.jks")
            storePassword 'SPICa27'
            keyAlias 'wuqi'
            keyPassword 'SPICa27'
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.signingConfig
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            //            shrinkResources true
            signingConfig signingConfigs.signingConfig
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        viewBinding true
    }

    tasks.withType(KotlinCompile).configureEach {
        kotlinOptions.freeCompilerArgs += ["-Xopt-in=kotlin.time.ExperimentalTime"]
        kotlinOptions.freeCompilerArgs += ["-Xopt-in=kotlinx.coroutines.ExperimentalCoroutinesApi"]
    }

    kotlinOptions {
        jvmTarget = '11'
    }
}

static String quotWrapper(key) {
    if (key == null) {
        return "\"\""
    }
    return "\"".concat(key).concat("\"")
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    api 'androidx.core:core-ktx:1.12.0'
    api platform('org.jetbrains.kotlin:kotlin-bom:1.8.0')
    api 'androidx.appcompat:appcompat:1.6.1'
    api 'com.google.android.material:material:1.10.0'
    api 'androidx.constraintlayout:constraintlayout:2.1.4'
    // 生命周期+协程
    api 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.2'
    api 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.2'
    api 'androidx.lifecycle:lifecycle-livedata-ktx:2.6.2'

    // activity & fragment ktx
    api 'androidx.fragment:fragment-ktx:1.6.2'
    api 'androidx.activity:activity-ktx:1.8.1'

    // 协程支持
    api 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.3'
    api 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3'

    // App startup
    api "androidx.startup:startup-runtime:1.1.1"
    // Timber 日志
    api 'com.jakewharton.timber:timber:5.0.1'

    // App startup
    api "androidx.startup:startup-runtime:1.1.1"
    api 'com.drakeet.multitype:multitype:4.3.0'

    //Json解析
    implementation 'com.squareup.moshi:moshi-kotlin:1.13.0'
    kapt 'com.squareup.moshi:moshi-kotlin-codegen:1.13.0'

    //network 网络请求相关
    implementation 'com.squareup.okhttp3:okhttp:5.0.0-alpha.11'
    implementation("com.squareup.okhttp3:logging-interceptor:5.0.0-alpha.11")
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:converter-moshi:2.9.0"
    implementation 'com.github.skydoves:sandwich:1.2.3'

    def roomVersion = "2.4.1"
    implementation("androidx.room:room-runtime:$roomVersion")
    kapt("androidx.room:room-compiler:$roomVersion")
    implementation("androidx.room:room-ktx:$roomVersion")

    implementation 'com.github.SilenceDut:jpinyin:v1.0'


    // hilt
    implementation 'com.google.dagger:hilt-android:2.44'
    kapt 'com.google.dagger:hilt-compiler:2.44'

    implementation 'no.danielzeller.blurbehindlib:blurbehindlib:1.0.0'
}

kapt {
    correctErrorTypes true
}

andResGuard {
    mappingFile = null
    use7zip = true
    useSign = true
    // 打开这个开关，会keep住所有资源的原始路径，只混淆资源的名字
    keepRoot = false
    // 设置这个值，会把arsc name列混淆成相同的名字，减少string常量池的大小
    fixedResName = "arg"
    // 打开这个开关会合并所有哈希值相同的资源，但请不要过度依赖这个功能去除去冗余资源
    mergeDuplicatedRes = true
    compressFilePattern = ["*.png",
                           "*.jpg",
                           "*.jpeg",
                           "*.gif",]
    sevenzip {
        artifact = 'com.tencent.mm:SevenZip:1.2.21'
    }
}

configurations {
    ktlint
}

dependencies {
    ktlint 'com.pinterest:ktlint:0.44.0'
}
task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "src/**/*.kt"
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "-F", "src/**/*.kt"
}